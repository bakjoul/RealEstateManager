plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.bakjoul.realestatemanager'
    compileSdk 34

    defaultConfig {
        applicationId "com.bakjoul.realestatemanager"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField 'String', 'GOOGLE_API_KEY', "\"${properties.getProperty('GOOGLE_API_KEY')}\""
        buildConfigField 'String', 'CURRENCY_API_KEY', "\"${properties.getProperty('CURRENCY_API_KEY')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-opt-in=kotlinx.coroutines.DelicateCoroutinesApi"
        ]
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'com.google.android.material:material:1.9.0'
    // Hilt
    implementation 'com.google.dagger:hilt-android:2.46.1'
    kapt 'com.google.dagger:hilt-compiler:2.46.1'
    // Room
    implementation "androidx.room:room-runtime:2.5.2"
    //noinspection KaptUsageInsteadOfKsp
    kapt "androidx.room:room-compiler:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"
    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    // Fragment
    implementation 'androidx.fragment:fragment-ktx:1.6.1'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    // Preferences DataStore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    // DotsIndicator
    implementation 'com.tbuonomo:dotsindicator:5.0'
    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // Firestore
    implementation 'com.google.firebase:firebase-firestore-ktx:24.8.1'
    // Firebase Auth
    implementation 'com.google.firebase:firebase-auth-ktx:22.1.2'
    // Google Auth
    implementation 'com.google.android.gms:play-services-auth:20.7.0'

    // CameraX core library using the camera2 implementation
    def camerax_version = "1.2.3"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.25'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
